plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "2.0.1"
}

dependencies {
    compile project(":kotlinx-coroutines-debug-manager")
    compile project(":kotlinx-coroutines-debug-transformer")
}

jar.enabled = false //prevent `build` task from empty jar creation

build.dependsOn shadowJar

shadowJar {
    baseName = "coroutines-debug-agent"
    classifier = null

    configurations = [project.configurations.compile]

    dependencies {
        include dependency("org.jetbrains.asm:asm-all.*")
        include dependency(":kotlinx-coroutines-debug.*")
        exclude dependency("org.jetbrains.kotlin:kotlin-stdlib-jre8")
    }

    relocate "org.jetbrains.org.objectweb.asm", "kotlinx.coroutines.debug.org.jetbrains.org.objectweb.asm"
}

jar {
    manifest {
        attributes "Premain-Class": "kotlinx.coroutines.debug.agent.Agent"
        attributes "Agent-Class": "kotlinx.coroutines.debug.agent.Agent"
        attributes "Can-Redefine-Classes": true
        attributes "Can-Retransform-Classes": true
    }
}